plugins {
    id 'idea'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'org.springframework.boot' version '2.6.6' apply false
    id 'com.google.cloud.tools.jib' version '3.2.1' apply false
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.9'
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "com.kivilev"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:${Versions.springBootDependencies}"
                mavenBom "org.testcontainers:testcontainers-bom:${Versions.testContainersBom}"
                mavenBom "com.google.protobuf:protobuf-bom:${Versions.protobufBom}"
            }

            dependency("com.fasterxml.jackson.core:jackson-databind:${Versions.jackson}")

            dependency("org.webjars:bootstrap:${Versions.bootstrap}")

            dependency("com.google.code.findbugs:jsr305:${Versions.jsr305}")
            dependency("com.github.spotbugs:spotbugs-annotations:${Versions.spotbugsAnnotation}")

            dependency("org.springframework.kafka:spring-kafka:${Versions.springKafka}")

            dependency("ch.qos.logback:logback-classic:${Versions.logBackClassic}")

            dependency("net.devh:grpc-server-spring-boot-starter:${Versions.grpcSpringBoot}")
            dependency("net.devh:grpc-client-spring-boot-starter:${Versions.grpcSpringBoot}")
        }
    }

    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '10.3.4'
        configFile file("$project.rootDir/checkstyle.xml")
        ignoreFailures = false
        maxWarnings = 0
        maxErrors = 0
    }

    apply plugin: 'com.github.spotbugs'
    spotbugs {
        showProgress = false
        effort = 'max'
        reportLevel = 'low'
        excludeFilter = file("$project.rootDir/spotbugs-exclude.xml")
    }
}
